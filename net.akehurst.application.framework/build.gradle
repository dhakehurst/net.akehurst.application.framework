/**
 * Copyright (C) 2016 Dr. David H. Akehurst (http://dr.david.h.akehurst.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}
subprojects {

	apply plugin: 'java'
	apply plugin: 'osgi'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'checkstyle'

	group 'net.akehurst.application.framework'
	version '1.0.alpha.19'

	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	checkstyle {
		toolVersion = '7.8'
		configFile = rootProject.file('checkstyle/checkstyle.xml')
	}

	task sourceJar(type: Jar) {
	    from sourceSets.main.allJava
	}

	publishing {
	    publications {
	        mavenJava(MavenPublication) {
	        	groupId "${group}"
	        	artifactId "${project.name}"
				version "${version}"
	        
	            from components.java
	            artifact sourceJar {
	                classifier "sources"
	            }
	        }
	    }
	}

	repositories {
		mavenCentral()
    	mavenLocal()
    	jcenter()
	}

	dependencies {
       	testCompile 'junit:junit:+'
	}


	task checkCodeHeaders {
		String header = """/**
 * Copyright (C) 2016 Dr. David H. Akehurst (http://dr.david.h.akehurst.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */"""
	
		FileTree tree = fileTree(dir: '.')
		tree.include '**/*.java'
		tree.include '**/*.gradle'
		tree.each {File f ->
			if (f.text.startsWith(header)) {
				// ok
			} else {
				println 'WARN: Code header does not match in file '+f
			}
		}
	}
	
	bintray {
		user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
		key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
		publications = ['mavenJava']
		pkg {
			repo = 'maven'
			name = 'net.akehurst.application.framework'
			userOrg = user
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/dhakehurst/net.akehurst.application.framework.git'
			version {
				name = "${project.version}"
			}
		}
	}
}